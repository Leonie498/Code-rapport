model{
  #longitudinal part
  for(i in 1:I){
    
    for(j in offset[i]:(offset[i+1]-1)){
      y[j]~dnorm(mu[j],tau[i])
      mu[j] <- inprod(beta[1:ncX], X[j, 1:ncX]) + inprod(b[i, 1:ncU], U[j, 1:ncU])
    }# end of j loop
    
    #random effect
    b[i, 1:ncU] ~ dmnorm(priorMean.b, prec.B)
    tau[i] <- pow(exp(log.sigma[i]), -2)
    sigma[i] <- exp(log.sigma[i]) #ecart type
    log.sigma[i] ~ dnorm(mu.log.sigma, tau.log.sigma)
  
  
  # Survival part
  etaBaseline[i] <- inprod(alpha[1:ncZ],Z[i, 1:ncZ]) + alpha.sigma*sigma[i]
  shareY[i] <- inprod(beta[1:ncX], Xtime[i, 1:ncX]) + inprod(b[i, 1:ncU], Utime[i, 1:ncU])
  log_h1[i] <- log(shape) + (shape - 1) * log(Time[i]) + etaBaseline[i] + alpha.assoc * shareY[i]
  for (k in 1:K) {
    shareY.s[i, k] <- inprod(beta[1:ncX], Xs[K * (i - 1) + k, 1:ncX]) + inprod(b[i, 1:ncU], Us[K * (i - 1) + k, 1:ncU])
    SurvLong[i, k] <- wk[k] * shape * pow(st[i, k], shape - 1) * exp(alpha.assoc * shareY.s[i, k])
  }
  log_S1[i] <- (-exp(etaBaseline[i]) * P[i] * sum(SurvLong[i, ]))
  logL[i] <- event[i]*log_h1[i] + log_S1[i]
  mlogL[i] <- -logL[i] + C
  zeros[i] ~ dpois(mlogL[i])
 }# end of i loop

  # Priors

  # priors for longitudinal parameters
  beta[1:ncX] ~ dmnorm(priorMean.beta[], priorTau.beta[, ])
  # prior random effects
  mu.log.sigma ~ dnorm(priorMean.log.sigma, precision)
  tau.log.sigma ~ dgamma(precision, precision)
  sigma2.log.sigma <- 1/tau.log.sigma
  prec.B[1:ncU, 1:ncU] ~ dwish(priorR.Sigma2[, ], priorK.Sigma2)
  covariance.b <- inverse(prec.B[,])
  #priors for survival parameters
  alpha[1:ncZ] ~dmnorm(priorMean.alpha[], priorTau.alpha[, ])
  shape ~ dgamma(precision, precision)
  alpha.sigma ~ dnorm(0,precision)
  alpha.assoc ~ dnorm(0, priorTau.alphaA)
}